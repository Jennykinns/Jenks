// SCRIPT: rbCheckProject.mel
// BY: Rob van den Bragt
// DATE:       March, 2004
// DESCRIPTION:      Checks if a loaded scene is part of the set project or not. If not, it will search or ask you to set the new project
// USAGE:	    Add this script to your userSetup.mel
//----------------------------------------------------------------------------------------------------------------------------------------

//--------------------------------------------------------------------------------------------------
// PROC: rbCheckProjectDir (check if opened scene is part of current project)
//--------------------------------------------------------------------------------------------------

global proc rbCheckProjectDir ()
{
string $setProject	= `workspace -query -rootDirectory`;	// current set project with "/" at end
string $scenePathAndName	= `file -query -expandName`;	// opened scene path + filename
string $sceneName	= basename ( $scenePathAndName, "");	// get scene name
string $scenePath	= `substitute $sceneName $scenePathAndName ""`;	// trim scene name to extract filename

string $sceneProject;

int $i = 0;	// 0 = unknown status, 1 = project directory found, 2 = unknown project location, 3 = ignore/project set correctly
string $s1 = $scenePath;
string $s2;

if ($sceneName == "untitled") $i = 3;	// no action when maya creates a new scene 

while ($i == 0) {
if (`filetest -d ($s1 + "scenes/")` == 0)
{
$s2 = `substitute "/$" $s1 ""`;	// trim "/" at end
$s1 = `match ".*/" $s2`;	// trim last dir name
if ($s1 == "") $i = 2;	// no scenes directory found
} 
else
{
$sceneProject = $s1;	// scenes directory found
if ($sceneProject == $setProject) $i = 3;	// scene project directory matches set project
else $i = 1;	// scene project directory does not match set project
}
}

// create dialog box for user to choose action

string $title	= " CheckProject v1.11 - rob@ChocolateTribe.co.za ";
string $result	= "";
string $bSceneProj = " Switch to Loaded Scene Project ";
string $bCurrentProj	= " Keep Current Project ";
string $bSetProj = " Set Project... ";

if ($i == 1)
{
string $message = ("This scene is part of another project.\n\n"
+ "Current Project :\n" + $setProject + "\n\n"
+ "Loaded Scene Project :\n" + $sceneProject + "\n");
$result = `confirmDialog -title $title -messageAlign "center" -message $message
-defaultButton $bCurrentProj -cancelButton $bCurrentProj -dismissString $bCurrentProj 
-button $bSetProj 
-button $bSceneProj 
-button $bCurrentProj`;
}
if ($i == 2)
{
string $message = ("This scene is part of another project. Maya is not able to locate the associated project automatically.\n\n"
+ "Current Project :\n" + $setProject + "\n");
$result = `confirmDialog -title $title -messageAlign "center" -message $message
-defaultButton $bCurrentProj -cancelButton $bCurrentProj -dismissString $bCurrentProj 
-button $bSetProj 
-button $bCurrentProj`;
}

// which button was clicked?

if ($result == $bCurrentProj)
{
// no change. keeps current project
print ("<rbCheckProject>    Ignored... Keeping current project: " + $setProject + "\n");
}
if ($result == $bSceneProj)
{	
// sets project to scene file
setProject $sceneProject;
print ("<rbCheckProject>    New location set for project: " + $sceneProject + "\n");
}
if ($result == $bSetProj)
{	
// opens dialog to set project
print ("<rbCheckProject>    Set project window opened.\n");
setProject "";
}
}


scriptJob -event SceneOpened "rbCheckProjectDir";